plugins {
    id 'java'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '7.0.4'
}

group = 'io.github.nifacy.c4patterns'
version = '1.0'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

repositories {
    mavenCentral()
}

spotless {
    java {
        eclipse().configFile("${rootProject.projectDir}/../common/formatter-config.xml")
    }
}

publishing {
    publications {
        debug(MavenPublication) {
            groupId = group
            artifactId = rootProject.name
            version = 'debug'
            from components.java
        }

        release(MavenPublication) {
            groupId = group
            artifactId = rootProject.name
            version = version
            from components.java
        }
    }

    repositories {
        mavenLocal()

        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Nifacy/c4-patterns")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
            }
        }
    }
}

tasks.register('formatCheck') {
    group = 'formatting'
    description = "Checks if code follows the formatting rules. Doesn't make changes"
    dependsOn 'spotlessCheck'
}

tasks.register('formatApply') {
    group = 'formatting'
    description = 'Applies formatting rules to all source files'
    dependsOn 'spotlessApply'
}

