plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.diffplug.spotless' version '7.0.4'
}

version = project.hasProperty("debug") ? 'debug' : releaseVersion

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.json:json:20240303'
    implementation 'com.structurizr:structurizr-dsl:3.2.1'
}

spotless {
    java {
        eclipse().configFile("${rootProject.projectDir}/../common/formatter-config.xml")
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

shadowJar {
    manifest {
        attributes(
                'Main-Class': 'io.github.nifacy.c4patterns.lens.App'
        )
    }
    archiveFileName = "${rootProject.name}-${project.version}.jar"
}

build {
    dependsOn shadowJar
}

tasks.register('formatCheck') {
    group = 'formatting'
    description = "Checks if code follows the formatting rules. Doesn't make changes"
    dependsOn 'spotlessCheck'
}

tasks.register('formatApply') {
    group = 'formatting'
    description = 'Applies formatting rules to all source files'
    dependsOn 'spotlessApply'
}

