plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.5'
    id 'io.freefair.aspectj' version '8.14'
    id 'com.diffplug.spotless' version '7.0.4'
}

group = 'com.patterns'
version = '1.0'

def patternSyntaxParserVersion = project.findProperty("deps.pattern-syntax-parser.version") ?: '1.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/Nifacy/c4-patterns")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
        }
    }
}

dependencies {
    compileOnly 'org.aspectj:aspectjrt:1.9.24'
    compileOnly 'com.structurizr:structurizr-dsl:3.2.1'
    implementation "io.github.nifacy.c4patterns:pattern-syntax-parser:${patternSyntaxParserVersion}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileAspectj {
    ajcOptions {
        compilerArgs += '-Xlint:adviceDidNotMatch=ignore'
    }
}

spotless {
    java {
        eclipse().configFile("${rootProject.projectDir}/../common/formatter-config.xml")
    }
}

shadowJar {
    archiveBaseName = rootProject.name
    archiveClassifier = ''
    archiveVersion = project.version

    dependencies {
        include(dependency('io.github.nifacy.c4patterns:pattern-syntax-parser:.*'))
    }
}

build {
    dependsOn shadowJar
}

tasks.register('formatCheck') {
    group = 'formatting'
    description = "Checks if code follows the formatting rules. Doesn't make changes"
    dependsOn 'spotlessCheck'
}

tasks.register('formatApply') {
    group = 'formatting'
    description = 'Applies formatting rules to all source files'
    dependsOn 'spotlessApply'
}
