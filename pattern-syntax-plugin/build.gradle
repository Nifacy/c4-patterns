plugins {
    id 'java-library'
    id 'com.gradleup.shadow' version '8.3.5'
}

group = 'com.patterns'
version = '1.0'

def patternLibVersion = project.findProperty("deps.pattern-lib.version") ?: 'v1.0'
def patternSyntaxParserVersion = project.findProperty("deps.pattern-syntax-parser.version") ?: 'v1.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/Nifacy/structurizr-templates")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
        }
    }
}

dependencies {
    implementation 'org.javassist:javassist:3.30.2-GA'
    implementation "com.patterns:pattern-syntax-parser:${patternSyntaxParserVersion}"
    implementation "com.patterns:pattern-lib:${patternLibVersion}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

shadowJar {
    archiveBaseName = rootProject.name
    archiveClassifier = ''
    archiveVersion = project.version

    manifest {
        attributes(
                'Premain-Class': 'com.patterns.syntax.PatternSyntaxPlugin',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true'
        )
    }

    dependencies {
        include(dependency('org.javassist:javassist:.*'))
        include(dependency('com.patterns:pattern-syntax-parser:.*'))
        include(dependency('com.patterns:pattern-lib:.*'))
    }
}

build {
    dependsOn shadowJar
}
